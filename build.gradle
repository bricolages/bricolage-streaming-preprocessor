buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:0.5.4.RELEASE"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.3.5.RELEASE"
    }
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'spring-boot'
apply plugin: 'maven'

group 'org.bricolages.streaming'

dependencyManagement {
    imports {
        mavenBom 'com.amazonaws:aws-java-sdk-bom:1.10.47'
    }
}

compileJava {
    options.deprecation = true
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}

///// dependency //////////////////////////////////////////////////////////////////

repositories {
    jcenter()
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.7'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.7'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.7'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.8.7'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-cbor', version: '2.8.7'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.8.7'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-csv', version: '2.8.7'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.9'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile group: 'org.apache.derby', name: 'derby', version: '10.13.1.1'
    compile group: 'com.amazonaws', name: 'aws-java-sdk-core', version: '1.11.27'
    compile group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: '1.11.27'
    compile group: 'com.amazonaws', name: 'aws-java-sdk-sqs', version: '1.11.27'
    compile group: 'org.springframework.data', name: 'spring-data-jpa', version: '1.10.2.RELEASE'
    compile group: 'org.springframework.data', name: 'spring-data-commons', version: '1.12.7.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: '1.3.5.RELEASE'
    compile group: 'postgresql', name: 'postgresql', version: '9.4.1208-jdbc42-atlassian-hosted'
    compile group: 'org.yaml', name: 'snakeyaml', version: '1.17'
    compile group: 'com.getsentry.raven', name: 'raven-logback', version: '7.7.0'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.16.14'
    testCompile group: 'org.projectlombok', name: 'lombok', version: '1.16.14'
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

///// xjar //////////////////////////////////////////////////////////////////////////////

jar.baseName = 'bricolage-streaming-preprocessor'
mainClassName = 'org.bricolages.streaming.Application'
springBoot.backupSource = false
bootRepackage.classifier = "executable"

///// DB migration //////////////////////////////////////////////////////////////////////

task migrate << {
    exec {
        executable "sh"
        args "-x", "-c", "cd db; ./ridgepole.sh --merge" + (project.hasProperty("dry-run") ? " --dry-run" : "")
    }
}

///// test ///////////////////////////////////////////////////////////////////////////////

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
    }
}

uploadArchives.repositories.mavenDeployer {
    repository url: "$System.env.MAVEN_REPO"
    pom.version = "1.0-SNAPSHOT"
}
